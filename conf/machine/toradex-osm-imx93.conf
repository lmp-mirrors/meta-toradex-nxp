#@TYPE: Machine
#@NAME: Toradex OSM iMX93
#@SOC: i.MX93
#@DESCRIPTION: Machine configuration for Toradex OSM i.MX93 SoM
#@MAINTAINER: Max Krummenacher <max.krummenacher@toradex.com>

MACHINE_NAME = "OSM-iMX93"

MACHINEOVERRIDES =. "tdx:mx93:"

IMX_DEFAULT_BSP = "nxp"

require conf/machine/include/imx-base.inc
# override imx-base.inc which sets this to cortexa55
DEFAULTTUNE:mx93-generic-bsp = "armv8a-crypto"
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc

# Don't include kernels in standard images
RRECOMMENDS:${KERNEL_PACKAGE_NAME}-base = ""

KERNEL_DTB_PREFIX = "freescale/"
KERNEL_DEVICETREE = " \
    ${KERNEL_DTB_PREFIX}imx93-toradex-osm-dev.dtb \
"
KERNEL_IMAGETYPE:mx93-generic-bsp = "Image.gz"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "toradex-osm-imx93_defconfig,sdcard"
SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_DTB_LOADADDRESS:mx93-generic-bsp = "0x9c400000"
UBOOT_DTBO_LOADADDRESS:mx93-generic-bsp = "0x9c440000"
UBOOT_ENTRYPOINT:mx93-generic-bsp = "0x94400000"
UBOOT_RD_LOADADDRESS = "0x9c800000"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
    lpddr4_dmem_1d_v202201.bin \
    lpddr4_dmem_2d_v202201.bin \
    lpddr4_imem_1d_v202201.bin \
    lpddr4_imem_2d_v202201.bin \
"

ATF_PLATFORM = "imx93"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_singleboot"
IMX_BOOT_SOC_TARGET = "iMX9"

IMAGE_BOOT_FILES:append = " boot.scr-${MACHINE};boot.scr"
WKS_FILE_DEPENDS:append = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script = "u-boot-distro-boot"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""
IMX_BOOT_SEEK = "32"

SERIAL_CONSOLES = "115200;ttyLP0"

MACHINE_FEATURES += "wifi bluetooth optee tpm2"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex"
PREFERRED_PROVIDER_virtual/kernel:preempt-rt = "linux-toradex"

IMX_DEFAULT_BOOTLOADER = "u-boot-toradex"
PREFERRED_VERSION_u-boot-toradex = "2024.04%"

IMAGE_CLASSES:append = " image_type_tezi"
IMAGE_FSTYPES += "teziimg"

UBOOT_BINARY_TEZI_EMMC = "imx-boot"
OFFSET_BOOTROM_PAYLOAD = "0"

TEZI_EXTERNAL_KERNEL_DEVICETREE_BOOT = ""

TORADEX_PRODUCT_IDS = "0219"
