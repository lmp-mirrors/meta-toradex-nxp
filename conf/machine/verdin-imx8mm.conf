#@TYPE: Machine
#@NAME: Toradex Verdin iMX8M Mini
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for Toradex Verdin iMX8M Mini SoM
#@MAINTAINER: Max Krummenacher <max.krummenacher@toradex.com>

MACHINE_NAME = "Verdin-iMX8MM"

MACHINEOVERRIDES =. "mx8:mx8m:mx8mm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

KERNEL_DEVICETREE = "freescale/fsl-imx8mm-verdin-dev.dtb "

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "verdin-imx8mm_config,sdcard"
SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_ENTRYPOINT = "0x80280000"
UBOOT_RD_LOADADDRESS = "0xA0000000"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = "lpddr4_pmu_train_1d_imem.bin lpddr4_pmu_train_1d_dmem.bin lpddr4_pmu_train_2d_imem.bin lpddr4_pmu_train_2d_dmem.bin"

# Set u-boot DTB
UBOOT_DTB_NAME = "fsl-imx8mm-verdin.dtb"

BOOT_SPACE = "65536"
IMAGE_BOOTLOADER = "imx-boot"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_evk_emmc_fastboot"

MACHINE_BOOT_FILES = "boot.scr"
IMAGE_BOOT_FILES_append = " ${MACHINE_BOOT_FILES}"
WKS_FILE_DEPENDS_append = " u-boot-distro-boot"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""
IMX_BOOT_SEEK = "33"

SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_FEATURES += "3g bluetooth alsa ext2 pci screen touchscreen usbgadget usbhost vfat wifi"


# Add additional firmware
MACHINE_FIRMWARE_append = " linux-firmware-ath10k firmware-imx-sdma-imx7d"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex"
PREFERRED_PROVIDER_virtual/kernel_preempt-rt = "linux-toradex-rt"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex"
PREFERRED_PROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils"
# Currently we use the nxp 2018.03 downstream for mx8 as some features are
# not yet available in 2019.03.
PREFERRED_VERSION_u-boot-toradex = "2018.03"
PREFERRED_VERSION_u-boot-toradex-fw-utils = "2018.03"

MACHINE_FIRMWARE_append = " linux-firmware-sd8997"
MACHINE_FIRMWARE_append = " firmware-imx-vpu-imx8"

PREFERRED_VERSION_wayland-protocols_mx8 = "1.16.imx"
PREFERRED_VERSION_weston_mx8 = "5.0.0.imx"

BOARD_TYPE = "verdin"

IMAGE_CLASSES_append = " image_type_tezi"
IMAGE_FSTYPES += "teziimg"

UBOOT_BINARY_TEZI_EMMC="imx-boot"
OFFSET_BOOTROM_PAYLOAD="2"

TORADEX_PRODUCT_IDS = "0055"
TORADEX_PRODUCT_IDS[0055] = "fsl-imx8mm-verdin-dev.dtb"
