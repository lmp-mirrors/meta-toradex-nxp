#@TYPE: Machine
#@NAME: Apalis iMX8
#@DESCRIPTION: Apalis iMX8 powered by a i.MX 8QuadMax SoC
#@MAINTAINER: Stefan Agner <stefan.agner@toradex.com>

# until we get it from meta-freescale
# https://github.com/Freescale/meta-freescale/commit/3dc8f2fe59866395681e1aa0565b1d98ca27043e
HOSTTOOLS_NONFATAL_append = " sha384sum"

MACHINE_NAME = "Apalis-iMX8"

SOC_SILICON_REV ?= ""

MACHINEOVERRIDES =. "mx8:mx8qm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE_use-head-next = "freescale/fsl-imx8qm-apalis-eval.dtb"
KERNEL_DEVICETREE = "freescale/fsl-imx8qm-apalis.dtb"

UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "apalis-imx8_config,sdcard"

BOOT_SPACE = "65536"
IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

MACHINE_BOOT_FILES = "boot.scr hdmitxfw.bin dpfw.bin"
IMAGE_BOOT_FILES_append = " ${MACHINE_BOOT_FILES}"
WKS_FILE_DEPENDS_append = " firmware-imx-8 u-boot-distro-boot"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex"
PREFERRED_PROVIDER_virtual/kernel_preempt-rt = "linux-toradex-rt"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex"
PREFERRED_PROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils"
# Currently we use the nxp 2018.03 downstream for mx8 as some features are
# not yet available in 2019.03.
PREFERRED_VERSION_u-boot-toradex = "2018.03"
PREFERRED_VERSION_u-boot-toradex-fw-utils = "2018.03"

MACHINE_FIRMWARE_append = " linux-firmware-pcie8997"
MACHINE_FIRMWARE_append = " firmware-imx-vpu-imx8"

IMXBOOT_TARGETS = "flash_b0"

PREFERRED_PROVIDER_virtual/libg2d_imxdpu = "imx-dpu-g2d"
PREFERRED_VERSION_weston_mx8 = "5.0.0.imx"

BOARD_TYPE = "apalis"

IMAGE_CLASSES_append = " image_type_tezi"
IMAGE_FSTYPES += "teziimg"

# The imx-boot container takes care for the i.MX 8 offset, so the container
# has to be flashed at offset 0 directly
UBOOT_BINARY_TEZI_EMMC="imx-boot"
OFFSET_BOOTROM_PAYLOAD="0"
do_image_teziimg[depends] += "imx-boot:do_deploy"
do_image_teziimg_distro[depends] += "imx-boot:do_deploy"

TORADEX_PRODUCT_IDS = "0037 0047 0048 0049"
TORADEX_PRODUCT_IDS[0037] = "fsl-imx8qm-apalis.dtb"
TORADEX_PRODUCT_IDS[0047] = "fsl-imx8qm-apalis.dtb"
TORADEX_PRODUCT_IDS[0048] = "fsl-imx8qm-apalis.dtb"
TORADEX_PRODUCT_IDS[0049] = "fsl-imx8qm-apalis.dtb"
